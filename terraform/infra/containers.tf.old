resource "proxmox_virtual_environment_container" "focal_template" {
  for_each = toset(local.config.lab.nodes)
  description = "Managed by Terraform"

  disk {
    datastore_id = local.container_ds
    size         = 10
  }

  initialization {
    dns {
      server = "1.1.1.1"
    }

    hostname = "ubuntu-focal-lxc-template"

    ip_config {
      ipv4 {
        address = "dhcp"
      }
    }

    user_account {
      keys     = [data.http.github_ssh_keys.response_body, data.vault_generic_secret.terraform.data["pm_public_key"]]
      password = "example"
    }
  }

  network_interface {
    name = "vmbr0"
  }

  node_name = each.key

  operating_system {
    template_file_id = proxmox_virtual_environment_file.ubuntu_container_template["focal"].id
    type             = "ubuntu"
  }

  pool_id  = proxmox_virtual_environment_pool.lab.id
  template = true
  vm_id    = (2030 + index(local.config.lab.nodes, each.value))

  tags = [
    "container",
    "example",
    "terraform",
  ]
}

resource "proxmox_virtual_environment_container" "containers" {
  for_each = local.config.containers
  disk {
    datastore_id = local.container_ds
  }

  clone {
    vm_id = proxmox_virtual_environment_container.focal_template[each.value.node].id
  }

  initialization {
    hostname = each.key
    
    ip_config {
      ipv4 {
        address = each.value.ipv4
      }
    }
  }

  node_name = each.value.node
  pool_id   = proxmox_virtual_environment_pool.lab.id
  vm_id     = each.value.vm_id
}
